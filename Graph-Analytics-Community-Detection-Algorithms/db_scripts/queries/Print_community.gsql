CREATE QUERY print_community(vertex<Prescriber> input_prescriber) FOR GRAPH MyGraph SYNTAX V2 { 
  /* 
  
  Returns edges of community given prescriber
  
  Sample input:
  input_prescriber: pre14 | pre25
  
  Using all Prescribers:
  (1) Select presribers where their community id
  	matches the input_prescriber and the 
	prescriber they refer
  (2) Return referral edges of the community
  
  */ 
	
	ListAccum<EDGE> @@edge_list;
	
	SumAccum<int> @@cid;
	
	start={input_prescriber};
	start=Select s from start:s post-accum @@cid += s.communityId;
	
	start = {Prescriber.*};
	
	start = select s from start:s-(referral>:e)-:t 
	        where s.communityId == @@cid and s.communityId == t.communityId
	        accum @@edge_list += e;
	
	print start;
	print @@edge_list;
}
